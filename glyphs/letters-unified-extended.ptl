$$include '../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../support/utils'
import [designParameters] from '../meta/aesthetics'

glyph-module

glyph-block LetterUnified-Extended : begin

	glyph-block-import CommonShapes
	glyph-block-import Overmarks
	glyph-block-import LetterUnified-Basic : VShape

	###########################################################################################
	# UNIFIED LETTERFORMS : GREEK ORIGINALS
	###########################################################################################
	sketch
		include MarkSet.e

		local middle : mix SB RightSB 0.42
		local fine   : adviceBlackness 3.25
		local k1 0.25
		local k2 0.5
		local k3 0.35
		include : dispiro
			widths.rhs fine
			g4   (RightSB - OX * 1.75) XH [heading Downward]
			bezcontrols k1 k2 k3 1 6
			g4.left.mid (middle + CorrectionOMidS) O [widths 0 Stroke]
			archv
			flat (SB + OX * 2) SmallSmoothB
			curl (SB + OX * 2) (XH - SmallSmoothA)
			arcvh
			g4.right.mid (middle - CorrectionOMidS) (XH - O) [widths 0 Stroke]
			bezcontrols (1 - k3) 0 (1 - k1) (1 - k2) 6
			g4   (RightSB - OX * 2) 0 [widths.heading 0 fine Downward]

		set-anchor 'overlay' BASE (middle - OX) (XH * OverlayPos)

		save 'alpha' 0x3B1

	define GammaBarLeft (SB * 1.5)
	define [GammaShape top] : glyph-construction
		include : VBarLeft GammaBarLeft 0 top
		include : HBarTop (GammaBarLeft - O) (RightSB - OX) top
		if SLAB : begin
			include : LeftwardTopSerif GammaBarLeft top SideJut
			include : LeftwardBottomSerif (GammaBarLeft + HVContrast * HalfStroke) 0 Jut
			include : RightwardBottomSerif (GammaBarLeft + HVContrast * HalfStroke) 0 MidJutSide
			include : tagged 'serifRT' : DownwardRightSerif (RightSB - OX) top VJut

	sketch # Gamma
		include MarkSet.capital
		include : GammaShape CAP
		save 'Gamma' 0x393
		save 'cyrGhe' 0x413

		branch
			include : nShoulder
				left   -- (GammaBarLeft + Stroke)
				right  -- RightSB
				top    -- CAP * HBarPos + Stroke / 4
				bottom -- 0
				sma    -- SmoothA
				smb    -- SmoothB
			include : VerticalHook (RightSB - HalfStroke * HVContrast) 0 (-HookX) Hook
			save 'cyrGheHook' 0x494

	sketch # cyrghe.upright
		include MarkSet.e
		include : GammaShape XH
		save 'cyrghe.upright'

		branch
			include : nShoulder
				left   -- (GammaBarLeft + Stroke)
				right  -- RightSB
				top    -- XH * HBarPos + Stroke / 4
				bottom -- 0
				sma    -- SmoothA * [Math.pow HBarPos 0.3]
				smb    -- SmoothB * [Math.pow HBarPos 0.3]
			include : VerticalHook (RightSB - HalfStroke * HVContrast) 0 (-HookX) Hook
			save 'cyrgheHook' 0x495

	sketch # cyrghe.SRB
		include [refer-glyph 'dotlessi'] AS_BASE
		include : refer-glyph "macronAbove"
		save 'cyrghe.SRB'

	italic-variant 'cyrghe' 0x433

	define [CyrGeShape top] : glyph-construction
		include : GammaShape top
		eject-contour 'serifRT'
		include : VBarRight (RightSB - OX) top (top + Accent)

	sketch # cyrGe
		include MarkSet.capital
		include : CyrGeShape CAP

		save 'cyrGe' 0x490

	sketch # cyrge
		include MarkSet.e
		include : CyrGeShape XH

		save 'cyrge' 0x491

	sketch # cyrGhayn
		include [refer-glyph 'cyrGhe'] AS_BASE
		include : HOverlayBar (SB * 0.3) [mix (SB + Stroke) (RightSB - Stroke) 0.55] (CAP * (1 - OverlayPos))

		save 'cyrGhayn' 0x492

	sketch # cyrghayn.upright
		include [refer-glyph 'cyrghe.upright'] AS_BASE
		include : HOverlayBar (SB * 0.3) [mix (SB + Stroke) (RightSB - Stroke) 0.55] (XH * (1 - OverlayPos))

		save 'cyrghayn.upright'

	sketch # cyrghayn.italic
		include [refer-glyph 'cyrghe.italic'] AS_BASE
		include : FlatSlashShape Middle (XH / 2) (OverlayStroke / 2) (-0.2) 0.75
		save 'cyrghayn.italic'
		save 'voicedlaryngenalspirant' 0x1D24

	italic-variant 'cyrghayn' 0x493


	sketch # gamma
		include MarkSet.p

		local xmid : [mix SB RightSB 0.46] + HalfStroke
		include : dispiro
			g4 (SB + 0.4 * Stroke * HVContrast) (XH - O) [widths.rhs]
			bezcontrols 0.33 0.17 1 0.62 6 important
			flat xmid 0
			curl xmid Descender [heading Downward]

		include : dispiro
			widths.rhs
			flat RightSB XH [heading Downward]
			curl RightSB (XH * 0.9) [heading Downward]
			quadcontrols 0 0.3 6
			g4   xmid 0 [widths.rhs : Stroke * 0.9]
		save 'gamma' 0x3B3

	sketch
		include MarkSet.b

		local xNeck : mix SB RightSB 0.07
		local bar : mix 0 XH 0.96
		local xOTLeft : mix SB RightSB 0.5
		local fine : Stroke * 0.5

		local sma : SmallSmoothA * bar / XH
		local smb : SmallSmoothB * bar / XH

		include : dispiro
			widths.rhs
			g4   [mix Middle RightSB 0.85] ([mix XH CAP 0.8] - Stroke) [heading Leftward]
			alsoThru 0.5 0.7
			g4   Middle (CAP - O - Stroke)
			archv 2
			g4   (xNeck + Stroke) [mix (CAP - O - Stroke) bar 0.5]
			arcvh 2
			g4   xOTLeft bar [heading Rightward]
			alsoThruThem : list {0.25 0.05} {0.5 0.13}
			flat (RightSB - OX) (bar - smb)
			curl (RightSB - OX) sma
			arcvh
			g4.left.mid (Middle + CorrectionOMidS) O
			archv
			flat (SB + OX) smb
			curl (SB + OX) (bar - sma)
			arcvh
			g4   xOTLeft (bar - (Stroke - fine) / 2) [widths 0 fine]

		save 'delta' 0x3B4
		save 'cyrbe.SRB'
		save 'latindelta' 0x1E9F

	turned 'turndelta' 0x18D 'delta' Middle (XH / 2) MarkSet.p

	sketch # cyrbe
		include MarkSet.b
		include : dispiro
			widths.rhs (Stroke * CThinB)
			flat (SB + OX + Stroke * (1 - CThinB) * HVContrast) SmallSmoothB
			curl (SB + OX + Stroke * (1 - CThinB) * HVContrast) (XH - SmallSmoothA)
			arcvh
			g4 (Middle - CorrectionOMidS) (CAP * 0.7 - O) [widths.rhs Stroke]
			archv
			flat (RightSB - OX) (XH - SmallSmoothB)
			curl (RightSB - OX) SmallSmoothA
			arcvh
			g4 (Middle + CorrectionOMidS) O
			archv
			flat (SB + OX) SmallSmoothB
			curl (SB + OX) (XH - SmallSmoothA)
			alsoThruThem [list {0.25 0.79} {0.5 0.87}] important
			g4   (RightSB - HalfStroke * HVContrast) CAP
		save 'cyrbe' 0x431

	define [SmallEpsilonShape top hook] : glyph-construction
		local stroke : adviceBlackness2 2.875 2 top
		local midx : mix SB RightSB 0.65
		local midy : top * OverlayPos
		local sma : top - [mix (midy + stroke / 2) (top - O - stroke) (SmoothB / (SmoothA + SmoothB))] - TanSlope * HVContrast * stroke
		local smb : [mix (stroke + O) (midy - stroke / 2) (SmoothB / (SmoothA + SmoothB))] + TanSlope * HVContrast * stroke
		local fine : stroke * CThin
		include : dispiro
			g4 (RightSB + O) (top - [fallback hook SHook]) [widths.lhs]
			hookstart (top - O)
			g4 (SB + (OX - O)) (top - sma)
			arcvh
			flat Middle (midy - (fine - stroke / 2)) [widths.heading fine 0 Rightward]
			curl midx (midy - (fine - stroke / 2)) [heading Rightward]
		include : dispiro
			flat midx (midy + (fine - stroke / 2)) [widths.heading fine 0 Leftward]
			curl Middle (midy + (fine - stroke / 2)) [heading Leftward]
			archv
			g4 (SB + (OX - O) + O * 2) smb [widths.lhs]
			hookend O
			g4 (RightSB - O) [fallback hook SHook]

	sketch # epsilon
		include MarkSet.e
		include : SmallEpsilonShape XH
		save 'epsilon' 0x3B5
		save 'latinepsilon' 0x25B
		include : FlipAround Middle (XH / 2)
		save 'turnepsilon' 0x1D08


	sketch # latinEpsilon
		include MarkSet.capital
		include : SmallEpsilonShape CAP
		save 'latinEpsilon' 0x190

	define [CyrZeShape top hook] : glyph-construction
		local stroke : adviceBlackness2 2.875 2 top
		local midx : mix RightSB SB 0.65
		local midy : top * OverlayPos
		local smb : top - [mix (midy + stroke / 2) (top - O - stroke) (SmoothA / (SmoothA + SmoothB))] + TanSlope * HVContrast * stroke
		local sma : [mix (stroke + O) (midy - stroke / 2) (SmoothA / (SmoothA + SmoothB))] - TanSlope * HVContrast * stroke
		local fine : stroke * CThin
		include : dispiro
			widths.rhs
			g4 (SB - O) (top - [fallback hook SHook])
			hookstart (top - O)
			g4 (RightSB - (OX - O)) (top - smb)
			arcvh
			flat Middle (midy - (fine - stroke / 2)) [widths.heading 0 fine Leftward]
			curl midx (midy - (fine - stroke / 2)) [heading Leftward]
		include : dispiro
			widths.lhs
			g4 (SB + O) [fallback hook SHook]
			hookstart O
			g4 (RightSB - (OX - O) - O * 2) sma
			arcvh
			flat Middle (midy + (fine - stroke / 2)) [widths.heading fine 0 Leftward]
			curl midx (midy + (fine - stroke / 2)) [heading Leftward]

	sketch # cyrZe
		include MarkSet.capital
		include : CyrZeShape CAP Hook
		save 'cyrZe' 0x417

	sketch # cyrze.BGR
		include MarkSet.p
		include : CyrZeShape CAP Hook
		apply-transform : Upright
		apply-transform : Translate 0 (XH - CAP)
		apply-transform : Italify
		save 'cyrze.BGR'

	sketch # cyrze
		include MarkSet.e
		include : CyrZeShape XH
		save 'cyrze' 0x437
		save 'revlatinepsilon' 0x25C

	sketch # Theta
		include MarkSet.capital
		include [refer-glyph 'O'] AS_BASE
		include : HBar (SB + Stroke * HVContrast - O * 3) (RightSB - Stroke * HVContrast + O * 3) CapMiddle
		save 'Theta' 0x398

	sketch # theta
		include MarkSet.b
		include : OShape CAP 0 (SB - O) (RightSB + O) Stroke (SmallSmoothA * 100) (SmallSmoothB * 100)
		include : HBar (SB + HalfStroke) (RightSB - HalfStroke) CapMiddle
		save 'theta' 0x3B8


	sketch # zeta
		include MarkSet.if
		include : HBar SB (RightSB + O) (CAP - HalfStroke)
		include : dispiro
			widths.rhs
			g4 (RightSB + O) (CAP - Stroke)
			bezcontrols 0.7 0.35 1 0.64 12
			g4.down.mid (SB + Stroke * HVContrast) [mix 0 CAP (0.54 * SmallSmoothB / (SmallSmoothA + SmallSmoothB))]
			arcvh
			g4 [mix SB RightSB 0.55] Stroke [heading Rightward]
			archv 12
			g4.down.mid RightSB [mix Descender Stroke 0.5] [heading Downward]
			arcvh 12
			flat (RightSB - (Stroke - [mix Descender Stroke 0.5]) * 1.1) Descender [heading Leftward]
			curl [Math.min (RightSB - (Stroke - [mix Descender Stroke 0.5]) * 1.1 - 1) [mix SB RightSB 0.5]] Descender [heading Leftward]

		save 'zeta' 0x3B6

	sketch # xi
		include MarkSet.if
		include : HBar SB (RightSB + O) (CAP - HalfStroke)

		local xbar : mix SB RightSB 0.85
		local ybar : mix 0 CAP 0.55
		include : dispiro
			widths.rhs
			g4.left.start xbar (CAP - Stroke)
			archv
			g4 (SB - O * 2 + Stroke * HVContrast) [mix (CAP - Stroke) ybar 0.6]
			arcvh
			g4.right.end (xbar - HalfStroke * TanSlope) ybar [widths.rhs (Stroke * [mix CThin 1 0.5])]
		include : dispiro
			widths.rhs (Stroke * [mix CThin 1 0.5])
			g4.left.start (xbar + HalfStroke * TanSlope) (ybar - Stroke)
			bezcontrols 0.7 0 1 0.6
			g4 (SB + Stroke * HVContrast) [mix 0 ybar 0.45] [widths.rhs]
			arcvh
			g4 [mix SB RightSB 0.5] Stroke
			archv
			g4.down.mid RightSB [mix Descender Stroke 0.5] [heading Downward]
			arcvh
			flat (RightSB - (Stroke - [mix Descender Stroke 0.5]) * 1.1) Descender [heading Leftward]
			curl [Math.min (RightSB - (Stroke - [mix Descender Stroke 0.5]) * 1.1 - 1) [mix SB RightSB 0.5]] Descender [heading Leftward]
		save 'xi' 0x3BE

	sketch
		include MarkSet.p
		include : refer-glyph "u.withBar"
		include : dispiro
			widths.rhs
			flat SB Descender [heading Upward]
			curl SB (Descender / 2) [heading Upward]
			straight.up.end SB SmallSmoothB [widths.heading 0 [adviceBlackness 4] Upward]
		if SLAB : begin
			include : LeftwardTopSerif SB XH SideJut

		save 'mu' 0x3BC
	sketch
		include MarkSet.p
		include : refer-glyph "u.withBar"
		local fine      : adviceBlackness 4
		local outStand  : SB * 0.75 + fine * 0.25
		local outStandY : SmallSmoothB / 2 - Descender * 0.6
		local yTurn     : SmallSmoothB / 2
		include : difference
			dispiro
				widths.lhs fine
				corner (SB + outStand) (yTurn + outStandY)
				corner (SB - outStand) (yTurn - outStandY)
			spiro-outline
				corner (SB - O) XH
				curl (SB - O) SmallSmoothB
				arcvh
				g4   (Middle + CorrectionOMidS) (-O)
				archv
				flat (RightSB + O) SmallSmoothA
				corner (RightSB + O) XH
		if SLAB : begin
			include : LeftwardTopSerif SB XH SideJut

		save 'uWithLightCentralizationStroke'

	sketch
		include MarkSet.capital
		include : HBar [mix SB RightSB 0.125] [mix RightSB SB 0.125] [mix 0 CAP 0.54]
		include : HBar (SB + OX) (RightSB - OX) (CAP - HalfStroke)
		include : HBar (SB + OX) (RightSB - OX) (0 + HalfStroke)
		if SLAB : begin
			include : DownwardLeftSerif (SB + OX) CAP VJut
			include : DownwardRightSerif (RightSB - OX) CAP VJut
			include : UpwardLeftSerif (SB + OX) 0 VJut
			include : UpwardRightSerif (RightSB - OX) 0 VJut

		save 'Xi' 0x39E

	glyph-block-export PiShape
	define [PiShape] : params [top bottom [shrinkrate 0.05] _fine noserif [endShrink 0] [df : DivFrame 1]] : glyph-construction
		local fine : fallback _fine Stroke
		local shrink : if (!noserif && SLAB) 0 (shrinkrate * (df.rightSB - df.leftSB))
		local endexpand : if (!noserif && SLAB) 0 (- endShrink * (df.rightSB - df.leftSB) || shrink / 2)
		include : HBarTop (df.leftSB - endexpand) (df.rightSB + endexpand) top fine
		include : VBarLeft (df.leftSB + shrink) bottom (top - fine / 2) fine
		include : VBarRight (df.rightSB - shrink) bottom (top - fine / 2) fine

		if (!noserif && SLAB) : begin
			include : CenterBottomSerif (df.leftSB + shrink + fine * 0.5 * HVContrast) bottom Jut fine
			include : CenterBottomSerif (df.rightSB - shrink - fine * 0.5 * HVContrast) bottom Jut fine
			include : CenterTopSerif (df.leftSB + shrink + fine * 0.5 * HVContrast) top Jut fine
			include : CenterTopSerif (df.rightSB - shrink - fine * 0.5 * HVContrast) top Jut fine

	sketch # Pi
		include MarkSet.capital
		include : PiShape CAP 0 (shrinkrate -- 0)
		save 'Pi' 0x3A0
		save 'cyrPe' 0x41F

	sketch # cyrpe.upright
		include MarkSet.e
		include : PiShape XH 0 (shrinkrate -- 0.05) (endShrink -- 0.05)
		save 'cyrpe.upright'

	sketch # cyrpe.SRB
		include [refer-glyph 'u'] AS_BASE
		include : refer-glyph "macronAbove"
		save 'cyrpe.SRB'

	italic-variant 'cyrpe' 0x43f
	sketch # pi
		include MarkSet.e
		include : PiShape XH 0 (noserif -- true) (shrinkrate -- 0.06)
		save 'pi' 0x3C0

	glyph-block-export SigmaShape
	define [SigmaShape df top bottom fine] : glyph-construction
		local cor : [Math.hypot (top - bottom) (df.rightSB - df.leftSB)] / (top - bottom)

		include : HBarTop df.leftSB df.rightSB top fine
		include : HBarBottom df.leftSB df.rightSB bottom fine

		local midx : df.middle - fine / 2
		start-from df.leftSB (bottom + fine)
		line-to (df.leftSB + fine * cor) (bottom + fine)
		line-to (midx + fine * cor) [mix bottom top 0.5]
		line-to (df.leftSB + fine * cor) (top - fine)
		line-to df.leftSB (top - fine)
		line-to midx [mix bottom top 0.5]
		reverse-last

		if SLAB : begin
			include : DownwardRightSerif df.rightSB top VJut
			include : UpwardRightSerif df.rightSB bottom VJut

	sketch # Sigma
		set-width Width
		include MarkSet.capital

		include : SigmaShape [DivFrame 1] CAP 0 Stroke
		save 'Sigma' 0x3A3
		save 'latinSigma' 0x1A9

	sketch # Phi
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital

		local y1 : CAP * [if SLAB 0.175 0.125]
		local y2 : CAP * [if SLAB 0.825 0.875]
		include : VBar df.middle 0 (y1 + HalfStroke)
		include : OShape y2 y1 (df.leftSB + O) (df.rightSB - O) df.mvs (SmoothA * df.div) (SmoothB * df.div)
		include : VBar df.middle y1 y2 df.mvs
		include : VBar df.middle (y2 - HalfStroke) CAP

		if SLAB : begin
			include : tagged 'serifMT' : CenterTopSerif df.middle CAP Jut
			include : tagged 'serifMB' : CenterBottomSerif df.middle 0 Jut
		save 'Phi' 0x3A6
		save 'cyrEf' 0x424

	sketch # taillessphi
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e

		local x0 : mix df.leftSB df.rightSB 0.1
		local y1 : mix 0 XH 0.75
		local x1 : df.leftSB + OX * 2
		local y3 : XH * 0.66
		local y4 : XH * 0.65
		include : dispiro
			widths.lhs df.mvs
			g4 x0 XH
			g4 x1 (XH * 0.55)
			arcvh
			g4 (df.middle + CorrectionOMidS) O
			archv
			g4 (df.width - x1) (XH * 0.55)
			arcvh 8
			g4.left.mid [mix (df.width - x1) (df.middle - df.mvs / 2 * HVContrast) 0.525] XH [heading Leftward]
			archv
			flat (df.middle - df.mvs / 2 * HVContrast) y3
			curl (df.middle - df.mvs / 2 * HVContrast) (df.mvs * 0.2) [heading Downward]
		save 'taillessphi' 0x2C77

	sketch # phi
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.p
		include : refer-glyph "taillessphi"
		include : VBar df.middle Descender (df.mvs * 0.2)
		save 'phi' 0x3C6

	sketch # varphi
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.if

		local y1 0
		local y2 XH
		include : VBar df.middle Descender (y1 + HalfStroke)
		include : OShape y2 y1 df.leftSB df.rightSB df.mvs (SmoothA * df.div) (SmoothB * df.div) nothing
		include : VBar df.middle y1 y2 df.mvs
		include : VBar df.middle (y2 - HalfStroke) CAP
		save 'varphi' 0x3D5

	sketch # cyref
		local df : DivFrame para.diversityM 3
		include [refer-glyph 'varphi'] AS_BASE ALSO_METRICS
		if SLAB : begin
			include : tagged 'serifMT' : LeftwardTopSerif df.middle CAP Jut
			include : tagged 'serifMB' : CenterBottomSerif df.middle Descender Jut
		save 'cyref' 0x444
		save 'latinphi' 0x278

	sketch # Psi
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital

		local y : CAP * 0.2
		local y2 : CAP * (1 - 0.2 / df.div)
		include : dispiro
			widths.lhs df.mvs
			flat df.leftSB y2 [heading Downward]
			curl df.leftSB (y + (SmoothB * df.div))
			arcvh
			g4   df.middle y [heading Rightward]
			archv
			flat df.rightSB (y + (SmoothA * df.div))
			curl df.rightSB y2 [heading Upward]
		include : VBar df.middle y CAP df.mvs
		include : VBar df.middle 0 (y + HalfStroke)
		if SLAB : begin
			include : tagged 'serifLT' : LeftwardTopSerif df.leftSB y2 SideJut
			include : tagged 'serifRT' : RightwardTopSerif df.rightSB y2 SideJut
			include : tagged 'serifMT' : CenterTopSerif df.middle CAP Jut
			include : tagged 'serifMB' : CenterBottomSerif df.middle 0 Jut

		save 'Psi' 0x3A8

	sketch # smcpPsi
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e

		local y : XH * 0.3
		local y2 XH
		include : dispiro
			widths.lhs df.mvs
			flat df.leftSB y2 [heading Downward]
			curl df.leftSB (y + (SmoothB * df.div))
			arcvh
			g4   df.middle y [heading Rightward]
			archv
			flat df.rightSB (y + (SmoothA * df.div))
			curl df.rightSB y2 [heading Upward]
		include : VBar df.middle y XH df.mvs
		include : VBar df.middle 0 (y + HalfStroke)
		if SLAB : begin
			include : tagged 'serifLT' : LeftwardTopSerif df.leftSB y2 SideJut
			include : tagged 'serifRT' : RightwardTopSerif df.rightSB y2 SideJut
			include : tagged 'serifMB' : CenterBottomSerif df.middle 0 Jut

		save 'smcpPsi' 0x1D2A

	sketch # psi
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.if

		include : dispiro
			widths.lhs df.mvs
			flat df.leftSB XH [heading Downward]
			curl df.leftSB SmallSmoothB
			arcvh
			g4   df.middle O [heading Rightward]
			archv
			flat df.rightSB SmallSmoothA
			curl df.rightSB XH [heading Upward]
		include : VBar df.middle 0 CAP df.mvs
		include : VBar df.middle Descender HalfStroke
		if SLAB : begin
			include : LeftwardTopSerif df.leftSB XH SideJut
			tag-contour 'serifLT'
			if (!para.isItalic) : begin
				include : tagged 'serifRT' : RightwardTopSerif df.rightSB XH SideJut
		save 'psi' 0x3C8


	define [OmegaShape top extend sma smb] : glyph-construction
		local fine : Stroke * CThin
		local x1 [mix SB RightSB 0.4]
		local x2 (Width - x1)
		local yattach : top * extend + Stroke
		include : dispiro
			g4   x1 (yattach - fine) [widths.heading 0 fine Leftward]
			archv
			flat SB (yattach - Stroke + smb) [widths 0 Stroke]
			curl SB (top - sma)
			arcvh
			g4   (Middle - CorrectionOMidS) (top - O)
			archv
			flat RightSB (top - smb)
			curl RightSB (yattach - Stroke + sma)
			arcvh
			g4   x2 (yattach - fine) [widths.heading 0 fine Leftward]
		include : VBarRight x1 0 (yattach) fine
		include : HBar SB x1 HalfStroke
		include : VBarLeft x2 0 (yattach) fine
		include : HBar x2 RightSB HalfStroke

	sketch # latinUpsilon1
		include MarkSet.capital
		include : OmegaShape CAP 0.08 SmoothA SmoothB
		save 'Omega' 0x3A9

		include : FlipAround Middle (CAP / 2)
		save 'latinUpsilon1' 0x1B1

	sketch # latinupsilon1
		include MarkSet.e
		include : OmegaShape XH 0.08 SmallSmoothA SmallSmoothB
		save 'latinomega' 0xAB65

		include : FlipAround Middle (XH / 2)
		save 'latinupsilon1' 0x28A


	sketch # omega
		local df : DivFrame para.diversityM 3
		set-width df.width
		include [DivFrame df.div].markSet.e

		local fine : adviceBlackness 3.25 df.div
		local mfine : fine * CThin
		local x0 : mix df.leftSB df.rightSB 0.1
		local y0 : XH - Stroke * 0.2
		local y1 : mix 0 XH 0.8
		local x1 : df.leftSB + OX
		local y3 : XH / 2
		local y4 : XH * 0.65

		include : dispiro
			widths.lhs fine
			g4 x0 y0
			g4 x1 (XH / 2)
			arcvh 8
			g4 [mix x1 (df.middle + fine / 2 * HVContrast) 0.5] O [heading Rightward]
			archv 8
			flat (df.middle + (mfine - fine / 2) * HVContrast) y3 [widths.heading mfine 0 Upward]
			curl (df.middle + (mfine - fine / 2) * HVContrast) y4 [heading Upward]
			end [function : set this.angles 4]
		include : dispiro
			widths.rhs fine
			g4 (df.width - x0) y0
			g4 (df.width - x1) (XH / 2)
			arcvh 8
			g4 [mix (df.width - x1) (df.middle - fine / 2 * HVContrast) 0.5] O [heading Leftward]
			archv 8
			flat (df.middle - (mfine - fine / 2) * HVContrast) y3 [widths.heading 0 mfine Upward]
			curl (df.middle - (mfine - fine / 2) * HVContrast) y4 [heading Upward]
			end [function : set this.angles 4]

		save 'omega' 0x3C9

	sketch # pomega
		local df : DivFrame para.diversityM 3
		set-width df.width

		include [refer-glyph 'omega'] AS_BASE
		include : HBar df.leftSB df.rightSB (XH - HalfStroke)

		save 'pomega' 0x3D6

	sketch # closeomega
		local df : DivFrame para.diversityM 3
		set-width df.width
		include [DivFrame df.div].markSet.e
		local fine : adviceBlackness 3.25
		local mfine : fine * CThin
		local x0 : df.middle - CorrectionOMidS
		local y0 : XH - O
		local y1 : mix 0 XH 0.45
		local x1 : df.leftSB + OX
		local y3 : XH / 2
		local y4 : XH * 0.65
		include : dispiro
			widths.rhs mfine
			flat (df.middle + (mfine - fine / 2) * HVContrast) y4 [heading Downward]
			curl (df.middle + (mfine - fine / 2) * HVContrast) y3 [heading Downward]
			arcvh 8
			g4 [mix x1 (df.middle + fine / 2 * HVContrast) 0.5] O [widths.heading 0 fine Leftward]
			archv 8
			g4 x1 y1
			arcvh
			g4 x0 y0
			archv
			g4 (df.width - x1) y1
			arcvh 8
			g4 [mix (df.width - x1) (df.middle - fine / 2 * HVContrast) 0.5] O [heading Leftward]
			archv 8
			flat (df.middle - (mfine - fine / 2) * HVContrast) y3 [widths.heading 0 mfine Upward]
			curl (df.middle - (mfine - fine / 2) * HVContrast) y4 [heading Upward]
			end [function : set this.angles 4]
		save 'closeomega' 0x277

	###########################################################################################
	# UNIFIED LETTERFORMS : CYRILLIC ORIGINALS
	###########################################################################################

	define [CyrYeriShape top _left _right _fine _jut] : glyph-construction
		local fine : fallback _fine Stroke
		local left : fallback _left SB
		local right : fallback _right RightSB
		local jut : fallback _jut Jut

		local bowl : top * 0.55 + HalfStroke
		local turnRadius : bowl * 0.45
		local turnbottom : mix 0 bowl (SmoothA / (SmoothA + SmoothB))
		local trShrink [Math.pow ((right - left) / (RightSB - SB)) 0.5]
		include : dispiro
			widths.lhs fine
			flat (left + Stroke * 0.2) 0 [heading Rightward]
			curl ([Math.max (left + fine * HVContrast) (right - turnRadius * trShrink)] + CorrectionOMidX * fine) 0
			archv 8
			g4   (right - OX) turnbottom
			arcvh 8
			flat ([Math.max (left + fine * HVContrast) (right - turnRadius * trShrink)] - CorrectionOMidX * fine) bowl
			curl (left + Stroke * 0.2) bowl [heading Leftward]
		include : VBarLeft left 0 top fine
		if SLAB : begin
			include : LeftwardBottomSerif left 0 (jut - fine / 2 * HVContrast)
			tag-contour 'serifYeriLB'
			include : CenterTopSerif (left + fine / 2 * HVContrast) top jut
			tag-contour 'serifYeriLT'

	define [RevCyrYeriShape top _left _right _fine _jut] : glyph-construction
		local fine : fallback _fine Stroke
		local left : fallback _left SB
		local right : fallback _right RightSB
		local jut : fallback _jut Jut

		local bowl : top * 0.55 + HalfStroke

		local turnRadius : bowl * 0.45
		local turnbottom : mix 0 bowl (SmoothB / (SmoothA + SmoothB))
		local trShrink [Math.pow ((right - left) / (RightSB - SB)) 0.5]
		include : dispiro
			widths.rhs
			flat (right - Stroke * 0.2) 0 [heading Leftward]
			curl (left + turnRadius * trShrink + CorrectionOMidX * fine) 0
			archv
			g4   (left + O) turnbottom [widths.rhs fine]
			arcvh
			flat (left + turnRadius * trShrink - CorrectionOMidX * fine) bowl [widths.rhs]
			curl (right - Stroke * 0.2) bowl [heading Rightward]
		include : VBarRight right 0 top fine
		if SLAB : begin
			include : RightwardBottomSerif right 0 (jut - fine / 2 * HVContrast)
			tag-contour 'serifYeriRB'
			include : CenterTopSerif (right - fine / 2 * HVContrast) top jut
			tag-contour 'serifYeriRT'

	sketch # cyrYeri
		include MarkSet.capital
		include : CyrYeriShape CAP SB RightSB

		save 'cyrYeri' 0x42C

	sketch # cyryeri
		include MarkSet.e
		include : CyrYeriShape XH SB RightSB

		save 'cyryeri' 0x44C

	sketch # cyrBe
		include [refer-glyph 'cyrYeri'] AS_BASE
		include : HBar SB [mix SB RightSB 0.9] (CAP - HalfStroke)
		if SLAB : begin
			include : DownwardRightSerif [mix SB RightSB 0.9] CAP VJut
		save 'cyrBe' 0x411
		save 'latinBe' 0x182

	sketch # ZhuangToneSix
		include [refer-glyph 'cyrYeri'] AS_BASE
		eject-contour 'serifYeriLT'
		local s : Math.max Stroke (XH * 0.1)
		start-from (SB - O) CAP
		line-to    SB CAP
		line-to    (SB - s) (CAP - s)
		line-to    SB (CAP - s * 2)
		line-to    (SB - O) (CAP - s * 2)
		reverse-last
		save 'ZhuangToneSix' 0x184

	sketch # latinDe
		include MarkSet.capital
		include : RevCyrYeriShape CAP
		include : HBarTop [mix RightSB SB 0.9] (RightSB + O) CAP
		if SLAB : begin
			include : DownwardLeftSerif [mix RightSB SB 0.9] CAP VJut
		save 'latinDe' 0x18B

	define [CyrYerShape top] : glyph-construction
		local left : if SLAB ([mix SB RightSB 0.35] - MVertStroke / 2 * HVContrast) [mix SB RightSB 0.2]
		local barleft : mix 0 SB [if SLAB 0.25 0.375]
		include : CyrYeriShape top left (RightSB - O * 2)
		include : HBarTop barleft (Stroke * 0.1 + left) top
		if SLAB : begin
			include : DownwardLeftSerif barleft top VJut (MVertStroke / HVContrast)

	sketch # cyrYer
		include MarkSet.capital
		include : CyrYerShape CAP
		save 'cyrYer' 0x42A

	sketch # cyryer
		include MarkSet.e
		include : CyrYerShape XH
		save 'cyryer' 0x44A

	define [CyrYeryShape top df] : glyph-construction
		local overshoot : O * 2
		include : CyrYeriShape top (df.leftSB + overshoot) [mix (df.rightSB - df.mvs * HVContrast) (Middle + df.mvs / 2 * HVContrast) (0.75 / df.div)] df.mvs (Jut * 0.75)
		include : VBarRight (df.rightSB - overshoot) 0 top df.mvs
		if SLAB : begin
			include : CenterTopSerif (df.rightSB - overshoot - df.mvs / 2 * HVContrast) top (Jut * 0.75)
			include : CenterBottomSerif (df.rightSB - overshoot - df.mvs / 2 * HVContrast) 0 (Jut * 0.75)

	sketch # cyrYery
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : CyrYeryShape CAP df
		save 'cyrYery' 0x42B
	sketch # cyryery
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : CyrYeryShape XH df
		save 'cyryery' 0x44B

	define [CyrIShape top] : glyph-construction
		local topstroke : adviceBlackness 4
		local halftopstroke : topstroke / 2
		include : dispiro
			widths.lhs
			flat RightSB 0 [heading Upward]
			curl RightSB (top * 0.4) [heading Upward]
			straight.up.end RightSB top [widths.heading topstroke 0 Upward]
		include : dispiro
			widths.lhs
			flat SB top [heading Downward]
			curl SB (top * 0.6) [heading Downward]
			straight.down.end SB 0 [widths.heading topstroke 0 Downward]
		include : dispiro
			flat (RightSB - halftopstroke) top [widths.heading 0 topstroke Downward]
			curl (SB + halftopstroke) 0 [widths.heading topstroke 0 Downward]
		include : AICyrISerifs top

	sketch # cyrI
		include MarkSet.capital
		include : CyrIShape CAP

		save 'cyrI' 0x418

	sketch # cyri.upright
		include MarkSet.e
		include : CyrIShape XH

		save 'cyri.upright'

	sketch # cyri.italic
		include [refer-glyph 'u'] AS_BASE

		save 'cyri.italic'

	italic-variant 'cyri' 0x438

	sketch # cyri.BGR
		include [refer-glyph 'u.withBar'] AS_BASE ALSO_METRICS
		save 'cyri.BGR'

	sketch # cyribreve.BGR
		include [refer-glyph 'u.withBar'] AS_BASE ALSO_METRICS
		include [refer-glyph 'breveAbove']
		save 'cyribreve.BGR'

	sketch # cyrigrave.BGR
		include [refer-glyph 'u.withBar'] AS_BASE ALSO_METRICS
		include [refer-glyph 'graveAbove']
		save 'cyrigrave.BGR'

	define [CyrTseShape top] : glyph-construction
		include : VBarLeft SB 0 top
		include : HBarBottom SB RightSB 0
		include : VBarRight RightSB 0 top
		include : CyrDescender RightSB (shift -- 0.05)
		if SLAB : begin
			include : AIVSerifs top
			include : LeftwardBottomSerif SB 0 SideJut
			include : RightwardBottomSerif RightSB 0 SideJut

	sketch # cyrTse
		include MarkSet.capital
		include : CyrTseShape CAP

		save 'cyrTse' 0x426

	sketch # cyrtse.upright
		include MarkSet.e
		include : CyrTseShape XH
		save 'cyrtse.upright'

	sketch # cyrtse.italic
		include MarkSet.e
		include : refer-glyph "u"
		eject-contour 'serifLT'
		include : CyrDescender RightSB (shift -- 0.05)
		save 'cyrtse.italic'

	italic-variant 'cyrtse' 0x446

	define [CyrDzheShape top] : glyph-construction
		include : VBarLeft SB 0 top
		include : HBarBottom SB RightSB 0
		include : VBarRight RightSB 0 top
		include : VBar Middle Descender Stroke
		if SLAB : begin
			include : AIVSerifs top
			include : LeftwardBottomSerif SB 0 SideJut
			include : RightwardBottomSerif RightSB 0 SideJut

	sketch # cyrDzhe
		include MarkSet.if
		include : CyrDzheShape CAP

		save 'cyrDzhe' 0x40F

	sketch # cyrdzhe.upright
		include MarkSet.p
		include : CyrDzheShape XH
		save 'cyrdzhe.upright'

	sketch # cyrdzhe.italic
		include [refer-glyph 'u'] AS_BASE
		include : refer-glyph "barBelow"
		save 'cyrdzhe.italic'

	italic-variant 'cyrdzhe' 0x45F


	define [CyrDeShape top] : glyph-construction
		local descenderOverflow : if SLAB SideJut ((RightSB - SB) * 0.075)
		local cutleft SB
		local cutright RightSB
		local topleft : mix cutleft cutright 0.15
		include : HBarBottom (cutleft - descenderOverflow) (cutright + descenderOverflow) 0
		include : VBarRight cutright 0 top
		include : halfXStrand false (topleft + Stroke * HVContrast) top (cutleft + HalfStroke * HVContrast * 0.8) HalfStroke 0.1 0.75 0.5
		include : VBarLeft (cutleft - descenderOverflow) (-LongJut + HalfStroke) 0
		include : VBarRight (cutright + descenderOverflow) (-LongJut + HalfStroke) 0

		if SLAB : then
			include : dispiro
				widths.rhs
				flat (topleft - descenderOverflow) top
				curl (cutright + descenderOverflow) top
		: else
			include : HBarTop topleft cutright top

	sketch # cyrDe
		include MarkSet.if
		include : CyrDeShape CAP

		save 'cyrDe' 0x414

	sketch # cyrde.upright
		include MarkSet.e
		include : CyrDeShape XH

		save 'cyrde.upright'

	sketch # cyrde.italic
		include MarkSet.b
		include : dispiro
			widths.lhs (Stroke * CThinB)
			flat (RightSB - OX - Stroke * HVContrast * (1 - CThinB)) SmallSmoothA
			curl (RightSB - OX - Stroke * HVContrast * (1 - CThinB)) (XH - SmallSmoothB)
			arcvh
			g4 (Middle - CorrectionOMidS) (CAP * 0.7 - O) [widths.lhs]
			archv
			flat (SB + OX) (XH - SmallSmoothA)
			curl (SB + OX) SmallSmoothB
			arcvh
			g4 (Middle + CorrectionOMidS) O
			archv
			flat (RightSB - OX) SmallSmoothA
			curl (RightSB - OX) (XH - SmallSmoothB)
			quadcontrols 0 0.8
			g4 (SB + Stroke * 1.1) CAP

		save 'cyrde.italic'

	italic-variant 'cyrde' 0x434

	sketch # cyrde.BGR
		include [refer-glyph 'g.singlestorey'] AS_BASE ALSO_METRICS
		save 'cyrde.BGR'

	define [CyrElShape top] : glyph-construction
		local cutleft : mix SB RightSB 0.135
		local cutleft2 : mix SB RightSB 0.075
		local cutright : mix SB RightSB 1
		include : VBarRight cutright 0 top
		include : HBar cutleft cutright (top - HalfStroke)
		include : LegShape
			ztop -- (cutleft        <> top)
			zbot -- ([mix SB 0 [if SLAB 1 0.75]] <> 0)
			xb -- cutleft2
		if SLAB : begin
			include : RightwardTopSerif cutright top SideJut
			include : LeftwardTopSerif cutleft top SideJut
			include : CenterBottomSerif (cutright - HalfStroke * HVContrast) 0 Jut

	sketch # cyrEl
		include MarkSet.capital
		include : CyrElShape CAP

		save 'cyrEl' 0x41B

	sketch # cyrel
		include MarkSet.e
		include : CyrElShape XH
		save 'cyrel' 0x43B


	define [CyrZheShape top midtop df] : glyph-construction
		local fine : adviceBlackness 3.3 df.div
		local midx : mix df.leftSB df.middle 0.3
		define [CyrAngleShape] : glyph-construction
			local cor HVContrast
			local overshoot : O * 3 * df.div
			start-from (df.leftSB + overshoot) 0
			line-to (df.leftSB + fine * cor + overshoot) 0
			line-to (midx + fine * cor) (top / 2)
			line-to (df.leftSB + fine * cor + overshoot) top
			line-to (df.leftSB + overshoot) top
			line-to midx (top / 2)
			reverse-last
		include : CyrAngleShape
		include : FlipAround df.middle (top / 2)
		include : CyrAngleShape
		include : VBar df.middle 0 midtop fine
		include : HBar midx (df.width - midx) (top / 2) fine
		if SLAB : begin
			define fineK 0.1
			include : LeftwardTopSerif     (df.leftSB + fine * fineK) top SideJut
			include : LeftwardBottomSerif  (df.leftSB + fine * fineK) 0 SideJut
			include : RightwardTopSerif    (df.rightSB - fine * fineK) top SideJut
			include : RightwardBottomSerif (df.rightSB - fine * fineK) 0 SideJut

	sketch # cyrZhe
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : CyrZheShape CAP CAP df
		save 'cyrZhe' 0x416

	composite [refer-glyph 'cyrZhe']
		CyrDescender ([DivFrame para.diversityM 3].rightSB - O * 2) (connex -- 0.5)
		into-unicode 0x496

	sketch # cyrzhe
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : CyrZheShape XH XH df
		save 'cyrzhe' 0x436

	composite [refer-glyph 'cyrzhe']
		CyrDescender ([DivFrame para.diversityM 3].rightSB - O * 2) (connex -- 0.5)
		into-unicode 0x497

	sketch # cyrzhe.BGR
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : CyrZheShape XH CAP df
		save 'cyrzhe.BGR'

	define [CyrBigYusShape top yp df] : glyph-construction
		local fine : adviceBlackness 3.3 df.div
		local cor HVContrast
		local midx : mix df.leftSB df.middle 0.3
		local midx2 : Math.min (df.middle - fine * cor) [mix df.leftSB df.middle 0.75]
		local yb : top * yp
		local tovershoot 2

		start-from (df.leftSB + O * 2) 0
		line-to (df.leftSB + fine * cor + O * 2) 0
		line-to (midx + fine * cor) yb
		line-to midx yb
		reverse-last

		start-from (midx2 + fine * cor) yb
		line-to (df.leftSB + fine * cor + O * tovershoot) top
		line-to (df.leftSB + O * tovershoot) top
		line-to midx2 yb
		reverse-last

		start-from (df.width - df.leftSB - O * 2) 0
		line-to (df.width - df.leftSB - fine * cor - O * 2) 0
		line-to (df.width - midx - fine * cor) yb
		line-to (df.width - midx) yb

		start-from (df.width - midx2 - fine * cor) yb
		line-to (df.width - df.leftSB - fine * cor - O * tovershoot) top
		line-to (df.width - df.leftSB - O * tovershoot) top
		line-to (df.width - midx2) yb


		include : VBar df.middle 0 yb fine
		include : HBarTop midx (df.width - midx) yb fine
		include : HBarTop (df.leftSB + O * tovershoot + HVContrast * fine * 0.99) (df.rightSB - O * tovershoot - HVContrast * fine * 0.99) top fine

	sketch # cyrbigYus
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : CyrBigYusShape CAP 0.575 df
		save 'cyrbigYus' 0x46A
	sketch # cyrbigyus
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : CyrBigYusShape XH 0.55 df
		save 'cyrbigyus' 0x46B

	define [CyrSmallYusShape top df straightBar] : glyph-construction
		local fine : adviceBlackness 3.3 df.div
		include : VShape top fine straightBar df.div
		eject-contour 'serifLT'
		eject-contour 'serifRT'
		include : FlipAround df.middle (top / 2)
		local p : if straightBar
			StrokeWidthBlend 0.24 0.24
			StrokeWidthBlend 0.16 0.16
		local bary (top / 2)
		if (SLAB && !para.isItalic) : set p : p * 1.33
		include : HBarTop [mix df.leftSB df.rightSB p] [mix df.rightSB df.leftSB p] bary fine
		include : VBar df.middle bary 0 fine
		if SLAB : begin
			include : tagged 'serifLB' : LeftwardBottomSerif df.leftSB 0 SideJut
			include : tagged 'serifRB' : RightwardBottomSerif df.rightSB 0 SideJut

	sketch # cyrsmallYus
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		branch
			include : CyrSmallYusShape CAP df true
			save 'cyrsmallYus.straight'
		branch
			include : CyrSmallYusShape CAP df false
			save 'cyrsmallYus.curly'
	sketch # cyrsmallyus
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		branch
			include : CyrSmallYusShape XH df true
			save 'cyrsmallyus.straight'
		branch
			include : CyrSmallYusShape XH df false
			save 'cyrsmallyus.curly'
	select-variant 'cyrsmallYus' 0x466 (follow -- 'Delta')
	select-variant 'cyrsmallyus' 0x467 (follow -- 'Delta')

	define [CyrShaShape top df] : glyph-construction
		include : union
			HBarBottom df.leftSB df.rightSB 0
			VBarLeft df.leftSB 0 top df.mvs
			VBarRight df.rightSB 0 top df.mvs
			VBar df.middle 0 top df.mvs

		if SLAB : begin
			include : tagged 'serifLB' : LeftwardBottomSerif df.leftSB 0 SideJut
			include : tagged 'serifRB' : RightwardBottomSerif df.rightSB 0 SideJut

			local jut : Jut * df.mvs / Stroke + O
			if (df.width > jut * 7) : begin
				include : tagged 'serifLT'
					CenterTopSerif (df.leftSB + 0.5 * df.mvs * HVContrast) top jut df.mvs
				include : tagged 'serifMT'
					CenterTopSerif df.middle top jut df.mvs
				include : tagged 'serifRT'
					CenterTopSerif (df.rightSB - 0.5 * df.mvs * HVContrast) top jut df.mvs
			: else : begin
				include : tagged 'serifLT' : LeftwardTopSerif df.leftSB top SideJut
				include : tagged 'serifRT' : RightwardTopSerif df.rightSB top SideJut

	sketch # cyrSha
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : CyrShaShape CAP df

		save 'cyrSha' 0x428

	sketch # cyrsha.upright
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : CyrShaShape XH df
		save 'cyrsha.upright'

	turned 'cyrsha.italic' null 'm.normal' [DivFrame para.diversityM 3].middle (XH / 2)
	italic-variant 'cyrsha' 0x448

	define [CyrShchaShape top df] : glyph-construction
		include : CyrShaShape top df
		#eject-contour 'serifRB'
		include : CyrDescender df.rightSB (shift -- 0.05)

	sketch # cyrShcha
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : CyrShchaShape CAP df

		save 'cyrShcha' 0x429

	sketch # cyrshcha.upright
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : CyrShchaShape XH df
		save 'cyrshcha.upright'

	sketch # cyrshcha.italic
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : refer-glyph "cyrsha.italic"
		include : CyrDescender df.rightSB (shift -- 0.05)
		save 'cyrshcha.italic'

	italic-variant 'cyrshcha' 0x449

	# Serbian pe and te
	sketch # cyrte.SRB
		local df : DivFrame para.diversityM 3
		set-width df.width
		include [refer-glyph 'cyrsha.italic'] AS_BASE
		include : refer-glyph "macronAbove"
		save 'cyrte.SRB'

	italic-variant 'cyrte' 0x442


	define [CyrCheShape top _barp] : glyph-construction
		local bar : top * [fallback _barp 0.5]
		include : VBarRight RightSB 0 top
		include : dispiro
			widths.lhs
			flat SB top [heading Downward]
			curl SB (bar + SmoothB - HalfStroke)
			arcvh
			flat Middle (bar - HalfStroke)
			curl (RightSB - 1) (bar - HalfStroke) [heading Rightward]
		if SLAB : begin
			include : CenterTopSerif (RightSB - HalfStroke * HVContrast) top Jut
			include : CenterTopSerif (SB + HalfStroke * HVContrast) top Jut
			include : CenterBottomSerif (RightSB - HalfStroke * HVContrast) 0 Jut

	sketch # cyrChe
		include MarkSet.capital
		include : CyrCheShape CAP [if SLAB 0.45 0.35]
		save 'cyrChe' 0x427
	composite [refer-glyph 'cyrChe'] [CyrDescender RightSB] [into-unicode 0x4B6]

	sketch # cyrche
		include MarkSet.e
		include : CyrCheShape XH [if SLAB 0.45 0.4]
		save 'cyrche' 0x447
	composite [refer-glyph 'cyrche'] [CyrDescender RightSB] [into-unicode 0x4B7]

	turned 'cyrShha' 0x4BA 'cyrChe' Middle (CAP / 2)

	sketch # cyrChevbar
		include [refer-glyph 'cyrChe'] AS_BASE
		local yc (CAP * [if SLAB 0.45 0.35] + Stroke * 0.1)
		include : VBar Middle (yc + LongJut * 0.8) (yc - LongJut * 0.8) OverlayStroke
		save 'cyrChevbar' 0x4B8

	sketch # cyrchevbar
		include [refer-glyph 'cyrche'] AS_BASE
		local yc (XH * [if SLAB 0.45 0.35] + Stroke * 0.1)
		include : VBar Middle (yc + LongJut * 0.8) (yc - LongJut * 0.8) OverlayStroke
		save 'cyrchevbar' 0x4B9


	define [CyrYuShape top xtop sma smb div] : glyph-construction
		local df : DivFrame div 3
		local xm : barmixL df.leftSB df.rightSB (df.mvs * HVContrast) [StrokeWidthBlend 0.4 0.45]
		include : VBarLeft df.leftSB 0 xtop df.mvs
		include : OShape top 0 xm df.rightSB df.mvs (sma * 0.7 * df.div) (smb * 0.7 * df.div)
		include : HBar (df.leftSB + 1) xm (top / 2)

		if SLAB : begin
			include : CenterTopSerif (df.leftSB + df.mvs / 2 * HVContrast) top (Jut * 0.75)
			include : CenterBottomSerif (df.leftSB + df.mvs / 2 * HVContrast) 0 (Jut * 0.75)

	sketch # cyrYu
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : CyrYuShape CAP CAP SmoothA SmoothB df.div
		save 'cyrYu' 0x42E

	sketch # cyryu
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : CyrYuShape XH XH SmallSmoothA SmallSmoothB df.div
		save 'cyryu' 0x44E

	sketch # cyryu.BGR
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.b
		include : CyrYuShape XH CAP SmallSmoothA SmallSmoothB df.div
		save 'cyryu.BGR'


	define [CyrLjeShape top df] : glyph-construction
		local rightSB : df.width - df.leftSB
		local l : df.leftSB + O
		local r : rightSB - O
		local middle : df.width / 2
		local xlefttop : mix l r 0.075
		local jut : Jut * 0.72
		set-width df.width
		include : LegShape
			ztop -- (xlefttop                      <> top)
			zbot -- ([mix l 0 [if SLAB 1 0.75]] <> 0)
			xb -- [mix l r 0.025]
			fine -- MVertStroke
		include : CyrYeriShape top (middle - MVertStroke / 2 * HVContrast) (r - O) MVertStroke jut
		include : HBarTop xlefttop middle top
		if SLAB : begin
			include : LeftwardTopSerif xlefttop top (jut - MVertStroke / 2 * HVContrast)

	sketch # cyrLje
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : CyrLjeShape CAP df
		save 'cyrLje' 0x409

	sketch # cyrlje
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : CyrLjeShape XH df
		save 'cyrlje' 0x459

	define [CyrNjeShape top df] : glyph-construction
		local rightSB : df.width - df.leftSB
		local xlefttop : mix df.leftSB df.rightSB 0.075
		local jut : Jut * 0.72
		local l : df.leftSB + O
		local r : rightSB - O
		local middle : df.width / 2
		set-width df.width
		include : VBarLeft l 0 top MVertStroke
		include : CyrYeriShape top (middle - MVertStroke / 2 * HVContrast) (r - O) MVertStroke jut
		include : HBar (df.leftSB + MVertStroke * 0.1) middle (top / 2)
		if SLAB : begin
			include : CenterTopSerif (l + MVertStroke / 2 * HVContrast) top jut
			include : CenterBottomSerif (l + MVertStroke / 2 * HVContrast) 0 jut

	sketch # cyrNje
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : CyrNjeShape CAP df
		save 'cyrNje' 0x40A

	sketch # cyrnje
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : CyrNjeShape XH df
		save 'cyrnje' 0x45A


	sketch # cyrTshe
		include MarkSet.capital

		local left : [mix SB RightSB 0.15] + OX
		local right : RightSB - OX

		include : nShoulder
			left -- (left + Stroke * HVContrast)
			right -- right
		include : VBarLeft left 0 CAP
		include : dispiro
			widths.rhs
			flat (SB + OX) CAP
			curl [mix left RightSB 0.475] CAP

		if SLAB : begin
			include : CenterBottomSerif (left + HVContrast * HalfStroke) 0 Jut
			include : CenterBottomSerif (right - HVContrast * HalfStroke) 0 Jut

		save 'cyrTshe' 0x40B

	sketch # cyrDje
		include MarkSet.capital

		local left : [mix SB RightSB 0.15] + OX
		local right: RightSB - OX * 1.5

		include : nShoulder
			left -- (left + Stroke * HVContrast)
			right -- right
			top -- XH
			bottom -- (Hook + HalfStroke + O)
			sma -- SmoothA
			smb -- SmoothB
		include : VBarLeft left 0 CAP
		include : dispiro
			widths.rhs
			flat (SB + OX) CAP
			curl [mix left RightSB 0.475] CAP
		include : VerticalHook (right - HalfStroke * HVContrast) (Hook + HalfStroke + O) [Math.max ((left - right) / 2 + HalfStroke) (-Hook * 1.2)] Hook

		if SLAB : begin
			include : LeftwardBottomSerif left 0 SideJut

		save 'cyrDje' 0x402

	sketch # cyrdje
		include MarkSet.if
		include : refer-glyph "cyrtshe"
		eject-contour 'serifRB'
		include : VerticalHook (RightSB - HalfStroke * HVContrast) 0 (-Hook * 1.2) Hook
		save 'cyrdje' 0x452

	###########################################################################################
	# UNIFIED LETTERFORMS : OTHER LATIN
	###########################################################################################
	do "eszet"
		local ymiddle : [mix 0 CAP 0.5] - HalfStroke
		local ymiddleCap : [mix 0 CAP 0.54] - HalfStroke
		local xmiddle : RightSB - ymiddle / 2 - Stroke * 0.75
		local xmiddleBot : RightSB - ymiddle / 2 - Stroke * 0.75
		local xfinal : Math.min
			xmiddle - 1 - Stroke * TanSlope
			Math.max (SB + Stroke * 2) [mix SB RightSB (1 / 4)]

		sketch # eszet.traditional
			set-width Width
			include MarkSet.if
			local l : SB * 1
			include : dispiro
				widths.lhs
				g4 ([mix SB RightSB 0.75] + HalfStroke * HVContrast) (CAP - Hook)
				hookstart (CAP - O)
				flat l XH
				curl l 0 [heading Downward]
			local t : mix 0 CAP 0.7
			local tm : [mix Descender t 0.625] + HalfStroke
			local tl : [mix l RightSB 0.35] + HalfStroke * HVContrast
			include : HBarTop (l + 1) (RightSB - HalfStroke * 1.2 - OX) t
			include : dispiro
				widths.rhs
				flat tl tm [heading Rightward]
				curl (tl + 1) tm [heading Rightward]
				g2 (RightSB - OX * 1.5) [mix Descender tm 0.70]
				g2 (RightSB - OX * 1.5) [mix Descender tm 0.67]
				alsoThru 0.5 0.75
				g4 [mix SB RightSB 0.35] Descender
			include : dispiro
				widths.center (Stroke * 1.1)
				corner tl (tm - Stroke) [heading Upward]
				corner (RightSB - HalfStroke * 1.2 - OX) t [heading Upward]
			piecewise
				para.isItalic : include : VerticalHook (l + HalfStroke * HVContrast) 0 (-Hook * 1.2) Hook
				SLAB : include : LeftwardBottomSerif SB 0 SideJut
			save 'eszet.traditional'

		sketch # eszet.sulzbacher
			include MarkSet.capital
			include : dispiro
				widths.rhs
				flat SB 0 [heading Upward]
				curl SB XH
				arcvh
				g4 (Middle + O) (CAP - O)
				archv
				g4 (RightSB + O * 2) [mix CAP ymiddle 0.47]
				g4.left.end xmiddle ymiddle [heading Leftward]
			include : dispiro
				widths.rhs
				g4.right.start xmiddle (ymiddle + Stroke) [heading Rightward]
				archv
				g4   (RightSB - O) [mix 0 (ymiddle + Stroke) 0.5]
				arcvh
				flat xmiddleBot 0
				curl xfinal 0 [heading Leftward]
			piecewise
				para.isItalic : include : VerticalHook (SB + HalfStroke * HVContrast) 0 (-Hook * 1.2) Hook
				SLAB : include : LeftwardBottomSerif SB 0 SideJut
			save 'eszet.sulzbacher'

		sketch # eszet.longsslig
			include MarkSet.capital

			define swOuter : adviceBlackness2 3 3 CAP
			define strokeCoeff : StrokeWidthBlend 0 1 swInner
			define sEndX : Math.max
				SB + swOuter * HVContrast + (RightSB - SB - Stroke) * 0.1
				mix SB RightSB 0.3
			define widthInner : Width - swOuter * HVContrast - (RightSB - SB - Stroke) * 0.1
			define swInner : adviceBlackness2 3 2 XH (widthInner / Width)
			define sTopY : XH + swInner / 8
			define smooth : SmoothBOf (swInner + (widthInner / Width) * ([adviceSSmooth XH (-1) swInner] - swInner)) widthInner
			define sTurnX : Math.max sEndX [mix SB RightSB 0.3]
			define sBotX : Math.max (sEndX + 1) [mix sEndX RightSB 0.30]
			define sTopX : sTurnX + (RightSB - sBotX)
			define sTopHookX : sTopX + 0.5 * swInner * HVContrast

			include : dispiro
				widths.rhs swOuter
				flat SB 0 [heading Upward]
				curl SB XH
				arcvh
				g4 ([mix SB sTopHookX 0.52] - CorrectionOMidX * swOuter) (CAP - O) [widths.rhs Stroke]
				straight.down.end sTopHookX (sTopY - swInner - O) [widths.heading 0 swInner Downward]

			include : dispiro
				widths.lhs swInner
				straight.left.start sTopX (sTopY - O) [widths.heading swInner 0 Leftward]
				archv
				g4   sTurnX (XH - smooth)
				SNeck swInner (slantCoeff -- 0.02) (tensionCoeff -- [StrokeWidthBlend 0.99 0.975]) (dist -- 0.025)
				g4   (RightSB - OX) (smooth) [widths 0 swInner]
				hookend O (sw -- swInner) (swItalicAdj -- swOuter)
				g4 (sEndX + OX) SHook

			piecewise
				para.isItalic : include : VerticalHook (SB + 0.5 * swOuter * HVContrast) 0 (-Hook * 1.2) Hook swOuter
				SLAB : include : LeftwardBottomSerif SB 0 SideJut

			save 'eszet.longsslig'

		select-variant 'eszet' 0xDF

		sketch # Eszet
			include MarkSet.capital
			local rightTopX : RightSB + O * 4
			include : dispiro
				widths.lhs
				flat rightTopX CAP
				curl Middle CAP
				archv
				flat SB XH
				curl SB 0 [heading Downward]
			include : dispiro
				widths.rhs
				g4   xmiddle (ymiddleCap + Stroke) [heading Rightward]
				archv
				g4   (RightSB - O * 2) [mix 0 (ymiddleCap + Stroke) 0.5]
				arcvh
				flat xmiddleBot 0
				curl xfinal 0 [heading Leftward]
			include : dispiro
				widths.rhs
				g4 rightTopX (CAP - Stroke)
				g4 xmiddle (ymiddleCap + Stroke) [widths Stroke 0]
			if SLAB : include : LeftwardBottomSerif SB 0 SideJut
			save 'Eszet' 0x1E9E


	sketch # AE
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital

		local eleft : df.middle - df.mvs * 0.25
		local turn : XH * 0.1

		# A half
		include : dispiro
			widths.rhs df.mvs
			flat df.leftSB 0 [heading Upward]
			curl df.leftSB (CAP * 0.1) [heading Upward]
			quadcontrols 0 0.3 6 unimportant
			g4   (eleft - HalfStroke) CAP [widths.rhs (df.mvs * 0.8)]

		start-from (eleft - HalfStroke) CAP
		line-to    eleft CAP
		line-to    eleft (CAP - df.mvs)
		include : HBarTop  ([mix df.leftSB (eleft - HalfStroke) (1/6)] + df.mvs * 0.5 * HVContrast)  (eleft + df.mvs / 2) (XH / 2) df.mvs

		# E half
		include : VBarLeft eleft 0 CAP df.mvs
		include : HBarTop (eleft - O) df.rightSB CAP
		include : HBar (eleft - O) (df.rightSB - df.mvs / 4) (CAP * 0.54)
		include : HBarBottom (eleft - O) df.rightSB 0
		if SLAB : begin
			include : CenterBottomSerif (df.leftSB + df.mvs / 2 * HVContrast) 0 Jut df.mvs
			include : DownwardRightSerif df.rightSB CAP VJut df.mvs
			include : UpwardRightSerif df.rightSB 0 VJut df.mvs
		save 'AE' 0xC6
		save 'cyrAE' 0x4D4

	define [OEShape top df] : glyph-construction
		local eleft df.middle

		# O half

		include : dispiro
			widths.lhs df.mvs 0
			straight.left.start eleft top [heading Leftward]
			archv
			flat  df.leftSB (top - SmoothA)
			curl  df.leftSB (SmoothB)
			arcvh
			straight.right.end eleft 0 [heading Rightward]

		# E half
		include : VBarLeft eleft 0 top df.mvs
		include : HBarTop (eleft - O) df.rightSB top
		include : HBar (eleft - O) (df.rightSB - df.mvs / 4) (top * 0.54)
		include : HBarBottom (eleft - O) df.rightSB 0

		if SLAB : begin
			include : DownwardRightSerif df.rightSB top VJut df.mvs
			include : UpwardRightSerif df.rightSB 0 VJut df.mvs

	sketch # OE
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : OEShape CAP df
		save 'OE' 0x152

	sketch # smcpOE
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : OEShape XH df
		save 'smcpOE' 0x276

	define [aeepart df] : glyph-construction
		local stroke : adviceBlackness2 2.875 3 XH df.div
		local eLeft : df.middle - stroke / 2 * HVContrast + 0.1
		local eMiddle : [mix eLeft (df.rightSB - OX * 2) 0.5] -  stroke * TanSlope
		local barbottom (XH * EBarPos - stroke / 2)

		local sma : SmoothAOf [Math.max (stroke * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)
		local smb : SmoothBOf [Math.max (stroke * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)
		include : difference
			union
				dispiro
					widths.lhs stroke
					flat (df.rightSB - OX * 2) barbottom [heading Upward]
					curl (df.rightSB - OX * 2) (XH - smb)
					arcvh
					g4   eMiddle (XH - O)
					archv
					flat eLeft (XH - sma)
					curl eLeft smb
					hookend O (tight -- true) (sw -- stroke)
					g4 (df.rightSB - OX * 2) (AHook + stroke * TanSlope) [heading Upward]
				HBarBottom (eLeft +  stroke / 2) (df.rightSB - OX * 2 -  stroke / 2) barbottom stroke
			Rect barbottom (barbottom - XH * 0.05) eMiddle df.width

	define [aeapart df] : glyph-construction
		local stroke : adviceBlackness2 2.875 3 XH df.div
		local bartop (XH * OverlayPos * 1.02 + stroke / 2)
		local abarRight : df.middle + stroke / 2 * HVContrast - 0.1
		local lowmiddle : mix (df.leftSB + OX * 2) abarRight 0.5
		local barsmooth : mix df.leftSB abarRight 0.7

		local sma : SmoothAOf [Math.max (stroke * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)
		local smb : SmoothBOf [Math.max (stroke * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)

		include : dispiro
			widths.rhs stroke
			g4 (df.leftSB + OX) (XH - AHook - stroke * TanSlope) [heading Upward]
			hookstart (XH - O) (tight -- true) (sw -- stroke)
			flat abarRight (XH - smb)
			curl abarRight smb
			arcvh
			g4 lowmiddle O [heading Leftward]
			archv
			g4 (df.leftSB + OX * 2) [mix 0 bartop 0.45]
			arcvh
			flat barsmooth bartop
			curl (abarRight - 1) bartop [heading Rightward]

	define [oeopart df] : glyph-construction
		local stroke : adviceBlackness2 2.875 3 XH df.div
		local abarRight : df.middle + stroke / 2 * HVContrast
		local sma : SmoothAOf [Math.max (stroke * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)
		local smb : SmoothBOf [Math.max (stroke * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)

		include : OShape XH 0 (df.leftSB + OX) (abarRight + OX) stroke sma smb

	sketch # ae
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : aeepart df
		include : aeapart df
		save 'ae' 0xE6
		save 'cyrae' 0x4D5
	turned nothing 0x1D02 'ae' [DivFrame para.diversityM].middle (XH / 2)

	sketch # oe
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.e
		include : aeepart df
		include : oeopart df
		save 'oe' 0x153
	turned nothing 0x1D14 'oe' [DivFrame para.diversityM].middle (XH / 2)

	sketch # db
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.b
		include : oeopart df
		include : FlipAround df.middle (XH / 2)
		include : oeopart df
		include : VBar df.middle (XH / 2) CAP MVertStroke
		if SLAB : begin
			include : LeftwardTopSerif (df.middle - MVertStroke / 2 * HVContrast) CAP SideJut

		save 'db' 0x238

	sketch # qp
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.p
		include : oeopart df
		include : FlipAround df.middle (XH / 2)
		include : oeopart df
		include : VBar df.middle Descender (XH / 2) MVertStroke
		if SLAB : begin
			include : CenterBottomSerif df.middle Descender Jut

		save 'qp' 0x239

	sketch # OU
		include MarkSet.capital
		include : OShape (CAP * HBarPos + HalfStroke) 0 SB RightSB nothing SmoothA SmoothB
		include : dispiro
			widths.lhs
			flat SB CAP [heading Downward]
			curl SB (CAP * HBarPos - O - HalfStroke + SmoothB)
			arcvh
			g4 (Middle + CorrectionOMidS) (CAP * HBarPos - O - HalfStroke + (Stroke * (1 - CThin))) [widths.lhs (Stroke * CThin)]
			archv
			flat RightSB (CAP * HBarPos - O - HalfStroke + SmoothA) [widths.lhs]
			curl RightSB CAP [heading Upward]

		save 'OU' 0x222

	sketch # ou
		include MarkSet.b
		local bar : StrokeWidthBlend (XH * 0.9) XH
		include : OShape bar 0 SB RightSB
		include : dispiro
			widths.lhs
			flat SB CAP [heading Downward]
			curl SB (bar - O - Stroke + SmoothB)
			arcvh
			g4 (Middle + CorrectionOMidS) (bar - O - Stroke + (Stroke * (1 - CThin))) [widths.lhs (Stroke * CThin)]
			archv
			flat RightSB (bar - O - Stroke + SmoothA) [widths.lhs]
			curl RightSB CAP [heading Upward]

		save 'ou' 0x223

	sketch # IJ
		include MarkSet.capital
		include : create-glyph : glyph-construction
			include : refer-glyph "I.straight"
			apply-transform : Translate (SB - Middle + HalfStroke * HVContrast) 0
		include : create-glyph : glyph-construction
			include : refer-glyph "J.shorthook"
			apply-transform : Translate JBalance2 0

		save 'IJ' 0x132

	sketch # ij
		include MarkSet.if
		include : create-glyph : glyph-construction
			include [refer-glyph 'dotlessi.straight'] AS_BASE
			include : refer-glyph "dotAbove"
			apply-transform : Translate (SB * 1.5 - Middle + HalfStroke * HVContrast) 0
		include : create-glyph : glyph-construction
			include [refer-glyph 'dotlessj.straight'] AS_BASE
			include : refer-glyph "dotAbove"
			apply-transform : Translate (RightSB - SB * 0.5 - Middle - JBalance - HalfStroke * HVContrast) 0
		save 'ij' 0x133

	glyph-block-export EzhShape
	define [EzhShape top bot pleft pright hookless] : glyph-construction
		local cor : 1.2 * HVContrast
		local yMidBar : mix bot top 0.6
		local ezhLeft : mix SB RightSB [fallback pleft 0.2]
		local ezhRight : mix SB RightSB [fallback pright 0.925]

		include : HBarTop SB ezhRight top

		start-from ezhLeft yMidBar
		line-to (ezhLeft + Stroke * cor) (yMidBar - HalfStroke)
		line-to (ezhLeft + Stroke * cor) yMidBar
		line-to ezhRight (top - Stroke)
		line-to (ezhRight - Stroke * cor) (top - Stroke)
		reverse-last

		include : dispiro
			widths.rhs
			flat ezhLeft yMidBar [heading Rightward]
			curl (Middle - CorrectionOMidS) yMidBar
			archv
			if hookless
			: then : list
				g4   (RightSB - OX) [mix yMidBar bot hookless] [heading Downward]
				g4   (RightSB - OX) ([mix yMidBar bot hookless] - 1) [heading Downward]
			: else : list
				g4   (RightSB - OX) [mix yMidBar bot : SmallSmoothB / (SmallSmoothA + SmallSmoothB)]
				hookend bot
				g4 SB (bot + Hook * (top - bot) / [fallback para.cap0 CAP])

		if SLAB : begin
			include : DownwardLeftSerif SB top VJut

	define [RevEzhShape top bot pleft pright hookless] : glyph-construction
		local cor : 1.2 * HVContrast
		local yMidBar : mix bot top 0.6
		local ezhRight : mix RightSB SB [fallback pleft 0.2]
		local ezhLeft : mix RightSB SB [fallback pright 0.925]
		include : HBarTop ezhLeft RightSB top

		start-from ezhRight yMidBar
		line-to (ezhRight - Stroke * cor) (yMidBar - HalfStroke)
		line-to (ezhRight - Stroke * cor) yMidBar
		line-to ezhLeft (top - Stroke)
		line-to (ezhLeft + Stroke * cor) (top - Stroke)

		include : dispiro
			widths.lhs
			flat ezhRight yMidBar [heading Leftward]
			curl (Middle + CorrectionOMidS) yMidBar
			archv
			g4   (SB + OX) [mix yMidBar bot (SmallSmoothA / (SmallSmoothA + SmallSmoothB))] [if [not hookless] important [heading Downward]]
			if hookless {} : list
				hookend bot
				g4 RightSB (bot + Hook * ((top - bot) / CAP))
		if SLAB : begin
			include : DownwardRightSerif RightSB top VJut

	sketch # Ezh
		set-width Width
		include MarkSet.capital
		include : EzhShape CAP 0
		save 'Ezh' 0x1B7
		save 'cyrEzh' 0x4E0

	sketch # ezh
		set-width Width
		include MarkSet.p
		include : EzhShape XH Descender
		save 'ezh' 0x292
		save 'cyrezh' 0x4E1

	sketch # revEzh
		include MarkSet.capital
		include : RevEzhShape CAP 0

		save 'revEzh' 0x1B8

	sketch # revezh
		include MarkSet.p
		include : RevEzhShape XH Descender

		save 'revezh' 0x1B9

	sketch # lyogh
		include MarkSet.if
		include : EzhShape XH Descender 0.4
		include : VBarLeft SB (XH * 0.1) CAP

		save 'lyogh' 0x26E

	sketch # ezhtail
		local b : mix Descender XH 0.25
		include : EzhShape XH b nothing nothing 0.5
		local y : mix [mix b XH 0.6] b 0.5
		include : dispiro
			widths.rhs
			g4 (RightSB - OX) y [heading Downward]
			g4 (RightSB - OX) (y - 1) [heading Downward]
			arcvh
			flat [mix SB RightSB 0.45] b
			curl [mix SB RightSB 0.4] b
			archv
			g4 (SB + Stroke * HVContrast) [mix (Descender + Stroke) b 0.5]
			arcvh
			flat [mix SB RightSB 0.4] (Descender + Stroke)
			curl RightSB (Descender + Stroke)

		save 'ezhtail' 0x1BA

	sketch # ezhcurlytail
		include MarkSet.e
		local p (SmallSmoothB / (SmallSmoothA + SmallSmoothB))
		include : EzhShape XH Descender nothing nothing p
		local fine : adviceBlackness2 5 3 (XH - Descender)
		local rinner : (XH * 0.4 - fine * 1.5) / 2
		local m1 : RightSB - Stroke * HVContrast - OX
		local x2 : RightSB - HalfStroke
		local y2 Descender
		include : dispiro
			widths.lhs
			g4 m1 [mix [mix Descender XH 0.6] Descender p] [heading Downward]
			g4 m1 ([mix [mix Descender XH 0.6] Descender p] - 1) [heading Downward]
			CurlyTail fine rinner m1 Descender SB x2 y2
		save 'ezhcurlytail' 0x293


	sketch # glottalstop
		include MarkSet.b
		include : dispiro
			widths.rhs
			g4 SB (CAP - Hook)
			hookstart (CAP - O)
			g4 RightSB (CAP - [adviceGlottalStopSmooth CAP 1])
			alsoThru.g2 0.5 0.5 important
			flat (Middle + HalfStroke * HVContrast) (XH * 0.3)
			curl (Middle + HalfStroke * HVContrast) 0 [heading Downward]
		if SLAB : begin
			include : CenterBottomSerif Middle 0 Jut

		save 'glottalstop' 0x294
		save 'capglottalstop' 0x241

	sketch # revglottalstop
		include MarkSet.b
		include : dispiro
			widths.lhs
			g4 RightSB (CAP - Hook)
			hookstart (CAP - O)
			g4 SB (CAP - [adviceGlottalStopSmooth CAP (-1)])
			alsoThru.g2 0.5 0.5 important
			flat (Middle - HalfStroke * HVContrast) (XH * 0.3)
			curl (Middle - HalfStroke * HVContrast) 0 [heading Downward]
		if SLAB : begin
			include : CenterBottomSerif Middle 0 Jut
		save 'revglottalstop' 0x295

	sketch # smallglottalstop
		include MarkSet.b
		include : dispiro
			widths.rhs
			g4 SB (XH - Hook)
			hookstart (XH - O)
			g4 RightSB (XH - [adviceGlottalStopSmooth XH 1])
			alsoThru.g2 0.5 0.5 important
			flat (Middle + HalfStroke * HVContrast) (XH * 0.15)
			curl (Middle + HalfStroke * HVContrast) 0 [heading Downward]
		if SLAB : begin
			include : CenterBottomSerif Middle 0 Jut

		save 'smallglottalstop' 0x242

	sketch # invglottalstopbar
		include MarkSet.e
		local smooth : adviceGlottalStopSmooth XH (-1 - TanSlope * (15 - (Width / 500) * 12) * [clamp 0 1 : linreg 126 1 135 0.5 Stroke])
		include : dispiro
			widths.lhs
			g4 RightSB (XH - Hook)
			hookstart (XH - O)
			g4.down.mid SB (XH - smooth)
			alsoThru.g2 0.5 0.5 important
			flat (Middle - HalfStroke * HVContrast) (XH * 0.15)
			curl (Middle - HalfStroke * HVContrast) 0 [heading Downward]
		include : HOverlayBar (Middle - LongJut * 0.6) (Middle + LongJut * 0.6) (XH * 0.25)
		include : FlipAround Middle (XH / 2)

		save 'invglottalstopbar' 0x1BE

	sketch # fineglottalstop
		include MarkSet.b
		local fine : markHalfStroke * 2
		include : dispiro
			widths.rhs fine
			g4 SB (CAP - Hook)
			hookstart (CAP - O)
			g4 RightSB (CAP - [adviceGlottalStopSmooth CAP 1])
			alsoThru.g2 0.5 0.5 important
			flat (Middle + fine / 2 * HVContrast) (XH * 0.3)
			curl (Middle + fine / 2 * HVContrast) 0 [heading Downward]
		if SLAB : begin
			include : CenterBottomSerif Middle 0 Jut fine

		save 'fineglottalstop'

	sketch # finerevglottalstop
		include MarkSet.b
		local fine : markHalfStroke * 2
		include : dispiro
			widths.lhs fine
			g4 RightSB (CAP - Hook)
			hookstart (CAP - O)
			g4 SB (CAP - [adviceGlottalStopSmooth CAP (-1)])
			alsoThru.g2 0.5 0.5 important
			flat (Middle - fine / 2 * HVContrast) (XH * 0.3)
			curl (Middle - fine / 2 * HVContrast) 0 [heading Downward]
		if SLAB : begin
			include : CenterBottomSerif Middle 0 Jut fine
		save 'finerevglottalstop'

	turned 'invglottalstop' 0x296 'revglottalstop' Middle (CAP / 2)

	sketch # glottalstopbar
		include [refer-glyph 'glottalstop'] AS_BASE
		include : HOverlayBar (Middle - LongJut * 0.6) (Middle + LongJut * 0.6) (CAP * 0.25)

		save 'glottalstopbar' 0x2A1

	sketch # revglottalstopbar
		include [refer-glyph 'revglottalstop'] AS_BASE
		include : HOverlayBar (Middle - LongJut * 0.6) (Middle + LongJut * 0.6) (CAP * 0.25)
		save 'revglottalstopbar' 0x2A2


	sketch
		include MarkSet.b

		include : OShape (CAP * 0.6) 0 SB RightSB
		local ymiddlea : (CAP * 0.6 + SmallSmoothA - SmallSmoothB) / 2
		include : dispiro
			widths.lhs
			g4.up.start (RightSB - OX) ymiddlea
			quadcontrols 0 0.8
			g4 (SB + Stroke * 1.1) CAP

		include : dispiro
			widths.center OverlayStroke
			flat [mix SB RightSB 0.1] [mix XH CAP (-0.1)]
			curl [mix SB RightSB 0.95] [mix XH CAP 0.3]

		save 'eth' 0xF0


	sketch # Thorn
		set-width Width
		include MarkSet.capital

		local bowlTop (CAP * 0.81)
		local bowlBottom (CAP * 0.19)

		local turn : mix bowlTop bowlBottom (SmoothB / (SmoothA + SmoothB))
		local turnRadius : (bowlTop - bowlBottom) / 2
		include : dispiro
			widths.rhs
			flat (SB * 1.25 + 1) bowlTop [heading Rightward]
			curl (RightSB - turnRadius - CorrectionOMidS) bowlTop
			archv
			g4   (RightSB - O) turn
			arcvh
			flat (RightSB - turnRadius + CorrectionOMidS) bowlBottom
			curl (SB * 1.25 + 1) bowlBottom [heading Leftward]

		include : VBarLeft (SB * 1.25) 0 CAP
		if SLAB : begin
			include : CenterBottomSerif (SB * 1.25 + HalfStroke * HVContrast) 0 Jut
			include : CenterTopSerif (SB * 1.25 + HalfStroke * HVContrast) CAP Jut

		save 'Thorn' 0xDE
		save 'Sho' 0x3F7

	sketch # thorn
		include : create-glyph : glyph-construction
			include : refer-glyph "b"
			eject-contour 'serifLB'
		include : create-glyph : glyph-construction
			include : refer-glyph "p"
			eject-contour 'bowl'
			eject-contour 'serifLT'
		include MarkSet.if
		save 'thorn' 0xFE

	sketch # sho
		include : create-glyph : glyph-construction
			include : refer-glyph "b"
			eject-contour 'serifLB'
			eject-contour 'serifLT'
		include : create-glyph : glyph-construction
			include : refer-glyph "p"
			eject-contour 'bowl'
			eject-contour 'serifLT'
			eject-contour 'serifLB'
		include MarkSet.if
		save 'sho' 0x3F8


	sketch # Hwair
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		include : VBarLeft df.leftSB 0 CAP df.mvs
		include : HBar SB df.middle (CAP / 2)
		include : dispiro
			widths.lhs df.mvs
			flat (df.middle - df.mvs * 0.5 * HVContrast) CAP [heading Downward]
			curl (df.middle - df.mvs * 0.5 * HVContrast) (SmallSmoothB * 0.6 * df.div)
			arcvh
			g4 [mix (df.middle - df.mvs * 0.5 * HVContrast) df.rightSB 0.5] O [heading Rightward]
			archv
			flat df.rightSB (SmallSmoothA * 0.6 * df.div)
			curl df.rightSB XH [heading Upward]
		if SLAB : begin
			local jut : Jut * 0.75
			include : CenterTopSerif (df.leftSB + df.mvs / 2 * HVContrast) CAP jut
			include : CenterBottomSerif (df.leftSB + df.mvs / 2 * HVContrast) 0 jut
			include : CenterTopSerif df.middle CAP jut

		save 'Hwair' 0x1F6

	sketch # hwair
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.b
		include : VBarLeft df.leftSB 0 CAP df.mvs
		include : dispiro
			nShoulderKnots (df.leftSB + df.mvs * HVContrast) (df.middle + df.mvs * 0.5 * HVContrast) (df.mvs * 0.4) nothing (XH * 0.51) (SmallSmoothA * 0.6 * df.div) (SmallSmoothB * 0.6 * df.div) df.mvs
			flat (df.middle + df.mvs * 0.5 * HVContrast) (XH * 0.5) [heading Downward]
			curl (df.middle + df.mvs * 0.5 * HVContrast) (SmallSmoothB * 0.6 * df.div)
			arcvh
			g4 [mix (df.middle - df.mvs * 0.5 * HVContrast) df.rightSB 0.5] O [widths.heading df.mvs 0 Rightward]
			archv
			flat df.rightSB (SmallSmoothA * 0.6 * df.div)
			curl df.rightSB XH [heading Upward]
		if SLAB : begin
			local jut : Jut * 0.75
			include : LeftwardTopSerif df.leftSB CAP (jut - df.mvs / 2 * HVContrast)
			if (!para.isItalic) : begin
				include : CenterBottomSerif (df.leftSB + df.mvs / 2 * HVContrast) 0 jut
				tag-contour 'serifLB'
		save 'hwair' 0x195


	sketch # Gha
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.capital
		local abarRight : df.middle + df.mvs / 2 * HVContrast
		local sma : SmoothAOf [Math.max (df.mvs * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)
		local smb : SmoothBOf [Math.max (df.mvs * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)
		include : OShape CAP 0 df.leftSB abarRight df.mvs sma smb
		include : VBarRight (df.rightSB - O) Descender CAP df.mvs
		include : dispiro
			widths.lhs df.mvs
			flat df.middle (CAP - smb) [heading Rightward]
			curl (df.middle + 1) (CAP - smb) [heading Rightward]
			alsoThru 0.5 0.15
			g4   (df.rightSB - O - df.mvs * HVContrast) CAP [widths 0 df.mvs]
		save 'Gha' 0x1A2

	sketch # gha
		local df : DivFrame para.diversityM 3
		set-width df.width
		include df.markSet.p
		local abarRight : df.middle + df.mvs / 2 * HVContrast
		local sma : SmoothAOf [Math.max (df.mvs * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)
		local smb : SmoothBOf [Math.max (df.mvs * 1.125) (SmallSmooth * 0.6 * df.div)] (Width * df.div)
		include : OShape XH 0 df.leftSB abarRight df.mvs sma smb
		include : VBarRight (df.rightSB - O) Descender XH df.mvs
		include : dispiro
			widths.lhs df.mvs
			flat df.middle (XH - smb) [heading Rightward]
			curl (df.middle + 1) (XH - smb) [heading Rightward]
			alsoThru 0.5 0.15
			g4   (df.rightSB - O - df.mvs * HVContrast) XH [widths 0 df.mvs]
		save 'gha' 0x1A3


	define [WynnShape bot top] : glyph-construction
		include : VBarLeft SB bot top
		include : dispiro
			widths.rhs (Stroke * CThin)
			g4 (SB + HVContrast * Stroke * (1 - CThin)) [mix bot top 0.8]
			alsoThru 0.55 0.85
			g4 Middle (top - O) [widths.heading 0 Stroke Rightward]
			archv
			g4 (RightSB - O) (top - [SmoothBOf (0.3 * (top - bot)) Width])
			alsoThru 0.25 0.45 important
			g4 (SB + Stroke * HVContrast) [mix bot top 0.1]

	sketch # Wynn
		include MarkSet.capital
		include : WynnShape 0 CAP

		save 'Wynn' 0x1F7

	sketch # wynn
		include MarkSet.p
		include : WynnShape Descender XH
		save 'wynn' 0x1BF


	define [YoghShape top bot] : glyph-construction
		include : dispiro
			widths.rhs
			g4 SB (top - Hook)
			hookstart (top - O)
			g4 RightSB (top - SmoothB)
			alsoThruThem {{0.5 0.75}}
			g4 ([mix SB RightSB 0.1] + Stroke * 0.25) ([mix bot top 0.425] - HalfStroke)
		include : dispiro
			widths.rhs
			g4 [mix SB RightSB 0.75] ([mix bot (top - SmoothB) 0.758] + HalfStroke)
			g4 RightSB ([mix bot (top - SmoothB) 0.475])
			alsoThruThem {{0.25 0.6} {0.5 0.81}}
			g4 SB (bot + O)

	sketch # Yogh
		include MarkSet.capital
		include : YoghShape CAP 0

		save 'Yogh' 0x21C

	sketch # yogh
		include MarkSet.p
		include : YoghShape XH Descender
		save 'yogh' 0x21D

	define [RamsHornShape bottom top] : glyph-construction
		local hf : [adviceBlackness 4] / 2
		local d : hf * 2 + Width * 0.05

		include : dispiro
			widths.center
			g4 (SB + HalfStroke * HVContrast + O) top [heading Downward]
			quadcontrols 1 0.7 16
			g4 (Middle + d - hf) (bottom + d * (1 - TanSlope * 0.5)) [widths hf hf]
			arcvh
			g4 (Middle + CorrectionOMidX * hf * 2) (bottom + hf)
			archv
			g4 (Middle - d + hf) (bottom + d * (1 + TanSlope * 0.5))
			quadcontrols 0 0.3 16
			g4 (RightSB - HalfStroke * HVContrast - O) top [widths.heading HalfStroke HalfStroke Upward]

	sketch # latingamma
		include MarkSet.p
		include : RamsHornShape Descender XH

		save 'latingamma' 0x263

	sketch # latinGamma
		include MarkSet.if
		include : RamsHornShape Descender CAP

		save 'latinGamma' 0x194

	sketch # ramshorn
		include MarkSet.e
		include : RamsHornShape 0 XH
		save 'ramshorn' 0x264

	### Ayin
	sketch # latinayin
		include MarkSet.e
		local k1 0.65
		local k2 0.7
		local zmidy (XH * 0.15)
		include : dispiro
			widths.lhs
			g4.right.start SB 0 [heading Rightward]
			g4 Middle zmidy
			g4.up.mid [mix Middle RightSB k1] (XH * k2) [heading Upward]
			arcvh
			g4.left.mid Middle (XH - O) [heading Leftward]
			archv
			g4.down.mid [mix Middle SB k1] (XH * k2) [heading Downward]
			g4 Middle zmidy
			g4.right.end RightSB 0 [heading Rightward]
		include : spiro-outline
			corner Middle (zmidy + 1)
			g4.up.mid ([mix Middle RightSB k1] - 1) (XH * k2)
			g4.left.mid Middle XH
			g4.down.mid ([mix Middle SB k1] + 1) (XH * k2)
			close
		reverse-last
		save 'latinayin' 0x1D25


	define [ErTail left w dohook] : glyph-construction
		local right Width
		local mid : mix left right 0.5
		local rise : (Width - left) * 0.3
		local sw : fallback w (markFine * 2)
		include : dispiro
			widths.rhs sw
			g2 (left - sw / 2 * HVContrast) (XH * 0.5)
			g2 (mid - sw * HVContrast) (XH * 0.5 + rise)
		include : dispiro
			widths.rhs sw
			flat mid (XH * 0.5 + rise) [heading Downward]
			curl mid (XH * 0.5 + [if dohook 0 (rise - 1)]) [heading Downward]
			if dohook {[hookend (XH * 0.5 - rise)]} {[arcvh]}
			g4 (right - [if dohook (markFine * 2) 0]) (XH * 0.5 - [if dohook (rise * 0.5) rise]) [if dohook nothing [heading Rightward]]
			#if dohook {[hookend (XH * 0.5 - rise)] [g4 right (XH * 0.5)]} {}

	sketch # rhotichook
		include : ErTail (-Stroke * HVContrast) Stroke true
		save 'rhotichook' 0x2DE

	if [not recursive] : let [thinfont : Widen {'schwa' 'revlatinepsilon'} 0.85 1] : begin
		sketch # er
			include MarkSet.e
			include thinfont.schwa
			include : ErTail (Width * 0.85 - SB - markFine * HVContrast * 1.25)
			save 'er' 0x25A

		sketch # revlatinepsiloner
			include MarkSet.e
			include thinfont.revlatinepsilon
			include : ErTail (Width * 0.85 - SB - markFine * HVContrast * 1.25)
			save 'revlatinepsiloner' 0x25D

	sketch # bidentalpercussive
		local g : create-glyph : glyph-construction
			include : HBarTop SB RightSB (CAP * 0.4)
			include : VBarLeft SB (CAP * 0.1) (CAP * 0.4)
			include : VBarRight RightSB (CAP * 0.1) (CAP * 0.4)
		include g
		apply-transform : Upright
		apply-transform : Translate 0 (CAP / 2)
		apply-transform : Italify
		include g
		save 'bidentalpercussive' 0x2AD
