import [add-common-feature pick-feature add-feature-lookup pick-lookup BeginLookupBlock EndLookupBlock] from "./table-util"
import [AnyCv] from "../support/gr"

define [FeatureName tag] : tag + '_cvss'
define [LookupName tag]  : 'lookup_cvss_' + tag

export : define [buildCVSS sink para glyphList] : begin
	if [not para.enableCvSs] : return nothing

	local rec : BeginLookupBlock sink

	# cvxx
	foreach [glyph : items-of glyphList]
		foreach [gr : items-of : AnyCv.query glyph] : if gr.tag : begin
			local feature : pick-feature sink [FeatureName gr.tag]
			add-common-feature sink feature

			local lookup : pick-lookup sink [LookupName gr.tag]
				begin {.type 'gsub_single' .subtables {{.}}}
			add-feature-lookup feature [LookupName gr.tag]

			set lookup.subtables.0.(glyph.name) [gr.get glyph]

	# ssxx
	foreach [{name composition} : pairs-of para.variants] : begin
		if (name.length === 4 && composition.__isComposite && [name.slice 0 2] === 'ss') : begin
			local feature : pick-feature sink [FeatureName name]
			add-common-feature sink feature

			foreach [{ch tag} : pairs-of composition.__cvmap]
				add-feature-lookup feature [LookupName tag]

	EndLookupBlock rec sink
